///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Знач КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("a all", Ложь, "Обновить все установленные пакеты");
	КомандаПриложения.Опция("f file", "", "Указать файл из которого нужно установить пакет");
	КомандаПриложения.Опция("l local", Ложь, "Обновление пакета в локальном каталоге oscript_modules");
	КомандаПриложения.Опция("nca not-create-app", Ложь, "признак отключения создания файла запуска");

	КомандаПриложения.Аргумент("PACKAGE", "", "Имя пакета в хабе. Чтобы установить конкретную версию, используйте ИмяПакета@ВерсияПакета")
						.ТМассивСтрок()
						.Обязательный(Ложь);

	// КомандаПриложения.Спек = "(-a | --all | -l | --local | -d | --dest )";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	
	ОбновлениеВЛокальныйКаталог = КомандаПриложения.ЗначениеОпции("local");
	ОбновлениеВсехПакетов = КомандаПриложения.ЗначениеОпции("all");
	ФайлПакетаУстановки = КомандаПриложения.ЗначениеОпции("file");
	МассивПакетовКОбновлению = КомандаПриложения.ЗначениеАргумента("PACKAGE");

	СоздаватьФайлыЗапуска = НЕ КомандаПриложения.ЗначениеОпции("not-create-app");

	РежимУстановки = РежимУстановкиПакетов.Глобально;

	Если ОбновлениеВЛокальныйКаталог = Истина Тогда
		РежимУстановки = РежимУстановкиПакетов.Локально;
	КонецЕсли;

	 Лог.Отладка("СоздаватьФайлыЗапуска: %1", СоздаватьФайлыЗапуска);

	НастройкаУстановки = РаботаСПакетами.ПолучитьНастройкуУстановки();
	НастройкаУстановки.СоздаватьФайлЗапуска = СоздаватьФайлыЗапуска;

	Если ОбновлениеВсехПакетов Тогда
		РаботаСПакетами.ОбновитьУстановленныеПакеты(РежимУстановки, , НастройкаУстановки);
	ИначеЕсли НЕ ПустаяСтрока(ФайлПакетаУстановки) Тогда
		РаботаСПакетами.УстановитьПакетИзФайла(ФайлПакетаУстановки, РежимУстановки, НастройкаУстановки);
	Иначе

		Для каждого ИмяПакета Из МассивПакетовКОбновлению Цикл
			РаботаСПакетами.ОбновитьПакетИзОблака(ИмяПакета, РежимУстановки, НастройкаУстановки);
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры